plugins {
    id 'java'
    id 'maven-publish'
    id 'jacoco'
    id "org.sonarqube" version "3.3"
    id 'signing'
}

group 'io.github.semantic-pie'
version '0.8.1'

sourceCompatibility = 17
targetCompatibility = 17

repositories {
    mavenCentral()
}

dependencies {

    implementation 'ch.qos.logback:logback-classic:1.4.11'
    implementation 'ch.qos.logback:logback-core:1.4.11'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation 'org.slf4j:slf4j-simple:2.0.0-alpha5'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    implementation 'jakarta.websocket:jakarta.websocket-api:2.0.0'
    implementation 'org.glassfish.tyrus.bundles:tyrus-standalone-client:2.0.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

// For java9 modules
//compileJava {
//    options.compilerArgs += ['--add-reads', 'JMantic.main=ALL-UNNAMED']
//    options.compilerArgs += ['-Xlint:unchecked'] // use to see more information about compiler warnings
//}

// Create jar archive with dependencies
jar {
    from {
        configurations.runtimeClasspath.filter { it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}

jacocoTestReport {
    dependsOn test

    reports {
        csv.enabled true
        xml.required = true
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }

}

sonarqube {
    properties {
        property "sonar.projectKey", "semanticpie_JMantic"
        property "sonar.organization", "semantic-pie"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {

    publications {
        gpr(MavenPublication) {
            groupId 'io.github.semantic-pie'
            artifactId 'jmantic'
            version '0.8.1'
            from components.java
            pom {
                name = 'JMantic'
                description = 'JMantic is a java library for working with the sc-memory of the OSTIS project over the websocket protocol Options';
                url = 'https://github.com/artrayme/JMantic'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'semantic-pie'
                        name = 'Semantic Pie'
                        email = 'ardonplay2@icloud.com'
                    }
                }
                scm {
                    developerConnection = 'scm:git:https://github.com/semantic-pie/JMantic.git'
                    url = 'https://github.com/semantic-pie/JMantic'
                }
            }
        }

        mavenJava(MavenPublication) {
            groupId 'io.github.semantic-pie'
            artifactId 'jmantic'
            version '0.8.1'
            from components.java
            pom {
                name = 'JMantic'
                description = 'JMantic is a java library for working with the sc-memory of the OSTIS project over the websocket protocol Options';
                url = 'https://github.com/artrayme/JMantic'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'semantic-pie'
                        name = 'Semantic Pie'
                        email = 'ardonplay2@icloud.com'
                    }
                }
                scm {
                    developerConnection = 'scm:git:https://github.com/semantic-pie/JMantic.git'
                    url = 'https://github.com/semantic-pie/JMantic'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/semantic-pie/jmantic")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = "Sonatype"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = System.getenv("SONATYPE_ACTOR")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

signing {
    def signingKey = System.getenv("GPG_KEY")
    def signingPassword = System.getenv("GPG_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
    sign publishing.publications.gpr
}


javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}